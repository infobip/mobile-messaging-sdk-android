buildscript {

    repositories {
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.4'
    }
}

plugins {
    id 'net.researchgate.release' version '2.8.1'
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
}

ext {
    mm_minSdkVersion = 16
    mm_compileSdkVersion = 31
    mm_targetSdkVersion = 31
    mm_buildToolsVersion = "31.0.0"
    mm_firebaseMessagingVersion = "22.0.0"
    mm_gmsLocationVersion = "18.0.0"
}

allprojects {
    repositories {
        mavenCentral()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
    }
}

subprojects {
    apply plugin: "maven"
    apply plugin: "maven-publish"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task updateVersionInReadme {
    doLast {
        def readmeFile = new File('README.md')
        def text = readmeFile.text
        readmeFile.withWriter { w ->
            w << text.replaceFirst('infobip-mobile-messaging-android-sdk:\\d+\\.\\d+\\.\\S+@aar', "infobip-mobile-messaging-android-sdk:$version@aar")
        }
    }
}

afterReleaseBuild.dependsOn updateVersionInReadme

// workaround for `net.researchgate.release` plugin issue:
// https://github.com/researchgate/gradle-release/issues/186
task build {}
build.dependsOn subprojects.findAll {!it.name.contains("demo")}.build

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            username = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSSRH_USERNAME')
            password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
            stagingProfileId.set(project.hasProperty('sonatypeStagingProfileId') ? project.property('sonatypeStagingProfileId') : System.getenv('SONATYPE_STAGING_PROFILE_ID'))
        }
    }
    connectTimeout = Duration.ofMinutes(3)
    clientTimeout = Duration.ofMinutes(3)
}

closeAndReleaseSonatypeStagingRepository.mustRunAfter subprojects.publish
afterReleaseBuild.dependsOn subprojects.publish, closeAndReleaseSonatypeStagingRepository

release {
    buildTasks = ['clean', 'build']
    git {
        requireBranch = ''
        pushToRemote = 'origin'
    }
}
