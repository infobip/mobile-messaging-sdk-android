buildscript {
    //Kotlin-Gradle-AGP compatibility matrix https://kotlinlang.org/docs/gradle-configure-project.html#apply-the-plugin
    ext.mm_kotlinVersion = "1.8.20"
    ext.mm_gradlePluginVersion = '7.4.0'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$mm_gradlePluginVersion"
    }
}

plugins {
    id 'net.researchgate.release' version '2.8.1'
    id 'maven-publish'
    id "io.github.gradle-nexus.publish-plugin" version "1.1.0"
    id 'org.jetbrains.kotlin.android' version "$mm_kotlinVersion" apply false
    id "org.sonarqube" version "4.0.0.2929"
    id "jacoco"
}

ext {
    mm_minSdkVersion = 16
    mm_compileSdkVersion = 34
    mm_targetSdkVersion = 34
    mm_buildToolsVersion = "34.0.0"
    mm_firebaseMessagingVersion = "22.0.0"
    mm_gmsLocationVersion = "18.0.0"
    mm_constraintLayoutVersion = "2.1.4"
    mm_appCompatVersion = "1.6.1"
    mm_materialVersion = "1.9.0"
    mm_coreKtxVersion = "1.10.1"
    mm_desugarJdkVersion = "1.1.8"
    mm_lifecycleVersion = "2.5.1"
}

allprojects {
    repositories {
        mavenCentral()
        google()
    }
}

//It's required to be applied here, to have `subprojects.publish` task available
subprojects {
    apply plugin: "maven-publish"
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task updateVersionInReadme {
    doLast {
        def readmeFile = new File('README.md')
        def text = readmeFile.text
        readmeFile.withWriter { w ->
            w << text.replaceFirst('infobip-mobile-messaging-android-sdk:\\d+\\.\\d+\\.\\S+@aar', "infobip-mobile-messaging-android-sdk:$version@aar")
        }
    }
}

task updateRtcUiVersionInReadme {
    doLast {
        def readmeFile = new File('infobip-rtc-ui/README.md')
        def text = readmeFile.text
        readmeFile.withWriter { w ->
            w << text.replaceFirst("'com.infobip:infobip-rtc-ui:\\d+\\.\\d+\\.\\S+'", "'com.infobip:infobip-rtc-ui:$version'")
        }
    }
}

afterReleaseBuild.dependsOn updateVersionInReadme, updateRtcUiVersionInReadme

// workaround for `net.researchgate.release` plugin issue:
// https://github.com/researchgate/gradle-release/issues/186

task build {}

build.dependsOn tasks.getByPath('infobip-mobile-messaging-android-chat-sdk:build')
build.dependsOn tasks.getByPath('infobip-mobile-messaging-android-geo-sdk:build')
build.dependsOn tasks.getByPath('infobip-mobile-messaging-android-inbox-sdk:build')
build.dependsOn tasks.getByPath('infobip-mobile-messaging-android-cryptor-migration:build')
build.dependsOn tasks.getByPath(':infobip-mobile-messaging-android-resources:build')
build.dependsOn tasks.getByPath(':infobip-mobile-messaging-android-sdk:build')
build.dependsOn tasks.getByPath(':infobip-mobile-messaging-api-java:build')
build.dependsOn tasks.getByPath(':infobip-rtc-ui:build')

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            username = project.hasProperty('ossrhUsername') ? project.property('ossrhUsername') : System.getenv('OSSRH_USERNAME')
            password = project.hasProperty('ossrhPassword') ? project.property('ossrhPassword') : System.getenv('OSSRH_PASSWORD')
            stagingProfileId.set(project.hasProperty('sonatypeStagingProfileId') ? project.property('sonatypeStagingProfileId') : System.getenv('SONATYPE_STAGING_PROFILE_ID'))
        }
    }
    connectTimeout = Duration.ofMinutes(3)
    clientTimeout = Duration.ofMinutes(3)
}

closeAndReleaseSonatypeStagingRepository.mustRunAfter subprojects.publish
afterReleaseBuild.dependsOn subprojects.publish, closeAndReleaseSonatypeStagingRepository

release {
    buildTasks = ['clean', 'build']
    git {
        requireBranch = ''
        pushToRemote = 'origin'
    }
}

sonar {
    properties {
        property "sonar.host.url", "https://sonar.ib-ci.com"
        property "sonar.sourceEncoding", "UTF-8"
        property "sonar.branch.name", System.getenv('BRANCH_NAME_TO_BUILD')
        property "sonar.projectVersion", project.version
        property "sonar.projectName", "infobip-mobile-messaging-android"
        property "sonar.projectKey" , "infobip-mobile-messaging-android"
        property "sonar.exclusions", "" +
                "infobip-mobile-messaging-android-demo/**/*, " +
                "infobip-mobile-messaging-android-cryptor-migration/**/*, " +
                "infobip-mobile-messaging-android-test/**/*, " +
                "infobip-rtc-ui/**/*"
        property "sonar.java.coveragePlugin", "jacoco"
        property "sonar.coverage.jacoco.xmlReportPaths", "" +
                "${project.projectDir}/infobip-mobile-messaging-android-geo-sdk/build/reports/jacoco/debugCoverage/debugCoverage.xml, " +
                "${project.projectDir}/infobip-mobile-messaging-android-inbox-sdk/build/reports/jacoco/debugCoverage/debugCoverage.xml, " +
                "${project.projectDir}/infobip-mobile-messaging-android-sdk/build/reports/jacoco/debugCoverage/debugCoverage.xml, " +
                "${project.projectDir}/infobip-mobile-messaging-api-java/build/reports/jacoco/test/jacocoTestReport.xml"
    }
}