buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$mm_gradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$mm_kotlinVersion"
    }
}

plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
}

android {
    namespace = "com.infobip.webrtc.ui"
    buildToolsVersion = mm_buildToolsVersion
    compileSdk = mm_compileSdkVersion

    publishing {
        multipleVariants {
            allVariants()
            withJavadocJar()
            withSourcesJar()
        }
    }
    defaultConfig {
        minSdkVersion mm_minSdkVersion
        targetSdkVersion mm_targetSdkVersion

        // since AGP 4.1 "VERSION_NAME" is not included anymore to BuildConfig
        // https://issuetracker.google.com/issues/154275579
        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "proguard-rules.pro"
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

dependencies {
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }

    implementation project(":infobip-mobile-messaging-android-sdk")

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$mm_kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$mm_coroutinesVersion"

    implementation "androidx.core:core-ktx:$mm_coreKtxVersion"
    implementation "androidx.constraintlayout:constraintlayout:$mm_constraintLayoutVersion"
    implementation "androidx.appcompat:appcompat:$mm_appCompatVersion"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$mm_lifecycleVersion"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$mm_lifecycleVersion"
    implementation "androidx.fragment:fragment-ktx:1.8.6"
    implementation "androidx.work:work-runtime-ktx:2.10.1"
    implementation "com.google.android.material:material:$mm_materialVersion"

    api ("com.infobip:infobip-rtc:2.5.8") {
        transitive = true
    }

    testImplementation "junit:junit:$mm_junitVersion"

    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$mm_desugarJdkVersion"
}

android.libraryVariants.configureEach { variant ->
    def name = variant.buildType.name
    def jar = project.tasks.create(name: "jar${name.capitalize()}", type: Jar) {
        Task javaCompileTask
        if (variant.hasProperty('javaCompileProvider')) {
            // Android 3.3.0+
            javaCompileTask = variant.javaCompileProvider.get()
        } else {
            javaCompileTask = variant.javaCompile
        }

        dependsOn javaCompileTask
        from javaCompileTask.destinationDirectory

        manifest {
            attributes(
                    "Bundle-Name": 'parse-android',
                    "Bundle-Version": project.version
            )
        }

        exclude '**/R.class'
        exclude '**/R\$*.class'
        exclude '**/Manifest.class'
        exclude '**/Manifest\$*.class'
        exclude '**/BuildConfig.class'
    }

    artifacts {
        archives jar
    }
}

ext {
    PUBLISH_ARTIFACT_ID = 'infobip-rtc-ui'
}
apply from: "${rootProject.projectDir}/publish-mavencentral.gradle"