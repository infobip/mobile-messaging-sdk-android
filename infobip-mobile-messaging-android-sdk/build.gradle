buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://maven.google.com/'
            name 'Google'
        }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.4'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'idea'

android {
    compileSdkVersion mm_compileSdkVersion
    buildToolsVersion mm_buildToolsVersion

    defaultConfig {
        minSdkVersion mm_minSdkVersion
        targetSdkVersion mm_targetSdkVersion
        versionCode 1
        versionName project.version
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'proguard-rules.pro'
    }

    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    api project(":infobip-mobile-messaging-api-java")
    api project(":infobip-mobile-messaging-android-resources")
    api "com.google.firebase:firebase-messaging:${mm_firebaseMessagingVersion}"
    api "com.google.android.gms:play-services-location:${mm_gmsLocationVersion}"
    api 'androidx.legacy:legacy-support-v4:1.0.0'
    api 'androidx.appcompat:appcompat:1.3.1'

    androidTestImplementation project(":infobip-mobile-messaging-android-test")
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation "junit:junit:4.12"
    androidTestImplementation "org.mockito:mockito-core:1.10.19"
    androidTestImplementation 'com.nanohttpd:nanohttpd:2.1.0'
    androidTestImplementation('org.skyscreamer:jsonassert:1.5.0') {
        exclude group: "org.json", module: "json"
    }

    testImplementation project(":infobip-mobile-messaging-android-test")
    testImplementation "junit:junit:4.12"
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def jar = project.tasks.create(name: "jar${name.capitalize()}", type: Jar) {
        Task javaCompileTask
        if (variant.hasProperty('javaCompileProvider')) {
            // Android 3.3.0+
            javaCompileTask = variant.javaCompileProvider.get()
        } else {
            javaCompileTask = variant.javaCompile
        }

        dependsOn javaCompileTask
        from javaCompileTask.destinationDir

        manifest {
            attributes(
                    "Bundle-Name": 'parse-android',
                    "Bundle-Version": project.version
            )
        }

        exclude '**/R.class'
        exclude '**/R\$*.class'
        exclude '**/Manifest.class'
        exclude '**/Manifest\$*.class'
        exclude '**/BuildConfig.class'
    }

    artifacts {
        archives jar
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier.set('sources')
}

task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

// Remove this when Gradle bug is fixed.
// https://github.com/gradle/gradle/issues/3170
task modifyPomBeforeUpload {
    doLast {
        def pom = "${project.buildDir}/poms/pom-default.xml"
        if (!file(pom).exists()) { install }
        def project = new XmlParser().parse(pom)
        project.dependencies
                .dependency
                .find { dependency -> dependency.artifactId[0].text() == 'infobip-mobile-messaging-android-resources' }
                .appendNode('type', 'aar')
        //noinspection UnnecessaryQualifiedReference
        groovy.xml.XmlUtil.serialize(project, new FileWriter(pom))
    }
}
modifyPomBeforeUpload.dependsOn(install)

ext {
    PUBLISH_ARTIFACT_ID = 'infobip-mobile-messaging-android-sdk'
}
apply from: "${rootProject.projectDir}/publish-mavencentral.gradle"

publish.dependsOn(modifyPomBeforeUpload)