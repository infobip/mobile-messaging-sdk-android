buildscript {
    repositories {
        mavenCentral()
        google()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:$mm_gradlePluginVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$mm_kotlinVersion"
    }
}

plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
}

android {
    namespace = "org.infobip.mobile.messaging.chat"
    buildToolsVersion = mm_buildToolsVersion
    compileSdk = mm_compileSdkVersion

    publishing {
        multipleVariants {
            allVariants()
            withJavadocJar()
            withSourcesJar()
        }
    }

    defaultConfig {
        minSdkVersion mm_minSdkVersion
        targetSdkVersion mm_targetSdkVersion
        versionCode 1
        versionName project.version

        // since AGP 4.1 "VERSION_NAME" is not included anymore to BuildConfig
        // https://issuetracker.google.com/issues/154275579
        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        consumerProguardFiles 'proguard-rules.pro'
        vectorDrawables.useSupportLibrary = true
    }

    buildFeatures {
        viewBinding true
        buildConfig true
    }

    buildTypes {
        debug {
            //needed for androidTests
            multiDexEnabled = true
            enableUnitTestCoverage true
            enableAndroidTestCoverage true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lint {
        abortOnError false
    }

    packagingOptions {
        resources.excludes.add("META-INF/*")
    }


}

dependencies {
    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }

    implementation project(":infobip-mobile-messaging-android-sdk")
    implementation project(":infobip-mobile-messaging-api-java")
    api project(":infobip-mobile-messaging-android-resources")

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$mm_kotlinVersion"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$mm_coroutinesVersion"

    implementation "androidx.core:core-ktx:$mm_coreKtxVersion"
    implementation "androidx.constraintlayout:constraintlayout:$mm_constraintLayoutVersion"
    implementation "androidx.appcompat:appcompat:$mm_appCompatVersion"
    implementation 'androidx.exifinterface:exifinterface:1.3.7'
    implementation "com.google.android.material:material:$mm_materialVersion"

    testImplementation project(":infobip-mobile-messaging-android-test")
    testImplementation "junit:junit:$mm_junitVersion"
    testImplementation "io.mockk:mockk:1.13.9"
    testImplementation "org.assertj:assertj-core:3.26.0"

    androidTestImplementation "androidx.test:runner:$mm_androidTestRunnerVersion"
    androidTestImplementation "androidx.test.ext:junit:$mm_androidJunitVersion"
    androidTestImplementation "junit:junit:$mm_junitVersion"
    androidTestImplementation "io.mockk:mockk-android:1.13.11"
    androidTestImplementation "com.android.support:multidex:$mm_multidexVersion"
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    def jar = project.tasks.create(name: "jar${name.capitalize()}", type: Jar) {
        Task javaCompileTask
        if (variant.hasProperty('javaCompileProvider')) {
            // Android 3.3.0+
            javaCompileTask = variant.javaCompileProvider.get()
        } else {
            javaCompileTask = variant.javaCompile
        }

        dependsOn javaCompileTask
        from javaCompileTask.destinationDirectory

        manifest {
            attributes(
                    "Bundle-Name": 'parse-android',
                    "Bundle-Version": project.version
            )
        }

        exclude '**/R.class'
        exclude '**/R\$*.class'
        exclude '**/Manifest.class'
        exclude '**/Manifest\$*.class'
        exclude '**/BuildConfig.class'
    }

    artifacts {
        archives jar
    }
}

ext {
    PUBLISH_ARTIFACT_ID = 'infobip-mobile-messaging-android-chat-sdk'
}
apply from: "${rootProject.projectDir}/publish-mavencentral.gradle"
apply from: "${rootProject.projectDir}/jacoco.gradle"